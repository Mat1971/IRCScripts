;Github Location and usage info:
;https://github.com/Mat1971/IRCScripts/tree/main/Handover

;Test: next active DJ
; print version number when it starts handover
;read through for no RMD links
;;;;;;;;;;;;;;;;

;This is the script for the Handover script in Sam.
;Storage location for this and future versions:

;Installation
;  The script is in two parts: one part in ADI IRC (this script), one part in SAM
;  This script accepts messages from Sam (running on same machine as AdiIRC) and sends them to the DJ's channel
;  Both scripts must run on the same PC! (ie your ADI and your Sam have to be the same computer)

;Compatibility
;  Tested with ADI 3.9 64 bit.    Should work on any fairly recent ADI
;

;Configuration:
;  Change your nick in the ** Configuration ** block below

;Testing
;  Type /TestSamListener into any window and it should paste a line into DJ channel
;  This tests that this script can receive the countdown from SAM
;
;Version History:
;    Version: 0.1  First released version

;Limitations
;    Will change your nick to your preferred nick on ALL servers that you're connected to (not just RMD servers)

;Requires AdiSamConfig script loaded!

;************************************
;********* Codes V0.1 ***************
;************************************

on *:START:{
  startSamSocket
}

on *:UNLOAD:{
  sockclose saminput
}

alias -l startSamSocket {
  sockclose saminput
  socklisten saminput 50501
}

on *:SOCKLISTEN:saminput:{
  diagnostics Incoming socket on saminput
  sockaccept incoming  
  if ($sock(incoming).ip != 127.0.0.1) {
    sockclose incoming
    return
  }  
  sockwrite -n incoming HTTP/1.0 200 OK
  sockwrite -n incoming Content-Type: text/html
  sockwrite -n incoming Content-Length: 39
  sockwrite -n incoming
  sockwrite -n incoming <HTML>Message received by AdiIRC</HTML>
}

on *:sockread:incoming:{
  diagnostics sockread on incoming
  if ($sock(incoming).ip != 127.0.0.1) return
  diagnostics sockread on incoming from localhost
  var %x
  sockread %x
  ;Check for | which might be an attempt to hack the listener:
  if (| isin %x) return
  diagnostics read data on http input: %x
  sockclose incoming
  handleHTTPrequest %x
}

 alias -l GetConsoleNumber { 
 ;eg $GetConsole(efnet)
 ;TODO add -l
   var %n =  $scon(0)
   while (%n > 0) {
       diagnostics ChangeConsole: checking for $scon(%n).servertarget contains $1      
       if ($1 isin $scon(%n).servertarget) return %n
      dec %n
      }
    echo -s GetConsoleNumber: server containing $1 not found
    return 1
 } 
 
 
alias -l handleHTTPrequest {
  ;Handle a line of form: GET /message?time=120&data=2+minutes+to+go HTTP/1.0
  ;Send message to DJ channel
  ;Change nick at time=30
  ;DJ is replaced with actual DJ nick if possible
  diagnostics http request received
  $regex($1-, /GET \/message.*[&?]data=(.*?)[ &])/)
  if ($regml(0) == 0) return
  ;replace +=>space   %3A=>:  %3F=>?   %2C=>   %2E=>.
  var %data = $replace( $regml(1),+, $chr(32))
  %data = $replace(%data , $chr(37) $+ 3A , $chr(58))
  %data = $replace(%data , $chr(37) $+ 3F , $chr(63))
  %data = $replace(%data , $chr(37) $+ 2C , $chr(44))
  %data = $replace(%data , $chr(37) $+ 2E , $chr(46))
  %data = $replace(%data,DJ,$getNextActiveDJ())
  echo -s Message at $time (ususally from SAM): %data
  scon $getconsolenumber($GetDJServer()) msg $GetDJChannel() %data

  ;nick change at 30s
  $regex($1-, /GET \/message.*[&?]time=(.*?)[ &])/)
  diagnostics regex result for time is $regml(0) and the value is $regml(1)
  if ( ($regml(0) != 0) && ($regml(1) == 30)) scon -a nick $GetNormalNick()
}

alias TestSamListener {
  ;This is called from the RMDPopup script, don't rename
  sockclose loopbacktest
  if ( $sock(saminput,0) == 1) {
    echo -s Sam listener port is running
  } else {
    echo -s Sam listener port is not running, starting it now
    startSamSocket
    if ( $sock(saminput,0) == 1) {
      echo -s  => Sam listener port started ok
    } else {
      echo -s  => Sam listener port failed to start!!! Is something else using port 50501?  Firewall issues?
      return
    }
  }
  echo -s Testing localhost loopbacktest connection .. (this should appear in the dj's channel)
  sockopen loopbacktest 127.0.0.1 50501
  sleep 1 /sockwrite -n loopbacktest GET /message?time=120&data=Test+message+from+AdiIRC HTTP/1.0
  sleep 2 /sockclose loopbacktest
}

alias -l diagnostics if (%diagnostics) echo -s RMD Countdown Link: $1-

alias getNextActiveDJ {
  ;get next active DJ (has DJ in their nick, not guesting, not merc, not self) or returns DJ
  diagnostics looking for DJ...
  scon $getconsolenumber($GetDJServer())
  var %channel = $GetDJChannel()
  var %result = DJ
  var %n = $nick(%channel,0)
  while (%n > 0) {
     var %nick = $nick(%channel,%n)
     diagnostics Checking nick %n = %nick
     var %valid = true
     if ($count(%nick,DJ) == 0 ) %valid = false
     if ($count(%nick,guest) > 0) %valid = false
     if ($count(%nick,DJ`Merc) > 0) %valid = false
     if (%nick == $me) %valid = false
     if (%valid == true) %result = %nick
     dec %n
  }
  diagnostics DJ in DJ channel is %result
  scon -r
  return %result
}

;Look for the server+channel with $simplehash(#channel) = 1009
alias simplehash {
  ;Return simple case-independent hash of $1
  var %acc =0
  var %n = $len($1)
  while (%n > 0) {
     %acc = $calc( $asc($mid($1,%n,1)) % 32 * %n + %acc )
     dec %n
  }
  return %acc
}
