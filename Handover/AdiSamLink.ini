alias GetSamLinkVersion return 0.2

;Project homepage: https://github.com/Mat1971/IRCScripts/tree/main/Handover

;Version History:
;   0.1  First released version
;   0.2  Auto config, get nick from ADI options, fixed $regex syntax

;Limitations
;    Will change your nick to your preferred nick on ALL servers that you're connected to (not just RMD servers)

on *:START:{
  startSamSocket
  %SamLinkPresent = 1
}

on *:UNLOAD:{
  sockclose saminput
  unset %SamLinkPresent
}

alias -l startSamSocket {
  ;Set up the world's smallest HTTP server on 50501
  sockclose saminput
  socklisten saminput 50501
}

on *:SOCKLISTEN:saminput:{
  diagnostics Incoming socket on saminput
  sockaccept incoming  
  if ($sock(incoming).ip != 127.0.0.1) {
    sockclose incoming
    return
  }  
  sockwrite -n incoming HTTP/1.0 200 OK
  sockwrite -n incoming Content-Type: text/html
  sockwrite -n incoming Content-Length: 39
  sockwrite -n incoming
  sockwrite -n incoming <HTML>Message received by AdiIRC</HTML>
}

on *:sockread:incoming:{
  diagnostics sockread on incoming
  if ($sock(incoming).ip != 127.0.0.1) return
  diagnostics sockread on incoming from localhost
  var %x
  sockread %x
  if (| isin %x) return
  diagnostics ADI SamLink: data read on http input
  sockclose incoming
  handleHTTPrequest %x
}

alias -l GetChannelFromHash {
  ;$GetDJChannelFromHash(num) returns space-separated scon number and channel name ($null if not found)
  var %n = $scon(0),%result
  while (%n > 0) {  
     scon %n
     var %c = $chan(0)
     while (%c > 0) {
         if ($simplehash($chan(%c)) == $1) %result = %n $chan(%c)
         dec %c
     }
     dec %n
  }  
  scon -r
  return %result
}

alias -l handleHTTPrequest {
  ;Handle a line of form: GET /message?time=120&data=2+minutes+to+go HTTP/1.0
  ;Send message to DJ channel
  ;Change nick at time=30
  ;DJ is replaced with actual DJ nick where possible
  diagnostics http request received
  var %dummy = $regex($1-, /GET \/message.*[&?]data=(.*?)[ &])/)
  if ($regml(0) == 0) return 
  var %serverchannel = $GetChannelFromHash(1009)
  diagnostics Console number and channel: %servernum %channel
  if (%serverchannel == $null) {
    echo -s Can't find DJ channel!  Are you in the DJ Channel?
    return
  }
  var %servernum = $token(%serverchannel,1,32)
  var %channel = $token(%serverchannel,2,32)
  
  ;replace +=>space   %3A=>:  %3F=>?   %2C=>   %2E=>.
  var %data = $replace( $regml(1),+, $chr(32))
  %data = $replace(%data , $chr(37) $+ 3A , $chr(58))
  %data = $replace(%data , $chr(37) $+ 3F , $chr(63))
  %data = $replace(%data , $chr(37) $+ 2C , $chr(44))
  %data = $replace(%data , $chr(37) $+ 2E , $chr(46))
  %data = $replace(%data,DJ,$getNextActiveDJ( %servernum,%channel))
  diagnostics Message at $time (ususally from SAM): %data
  
  scon %servernum msg %channel %data

  ;nick change at 30s:
  var %dummy = $regex($1-, /GET \/message.*[&?]time=(.*?)[ &])/)
  diagnostics regex result for time is $regml(0) and the value is $regml(1)  
  var %normalNick = $readini($adiircini, QuickConnect, Usernick)
  diagnostics normalNick = %normalNick
  if (%normalNick == $null) %normalNick = MyUsualNick
  if ( ($regml(0) != 0) && ($regml(1) == 30)) {
       scon -a nick %normalNick
       scon %servernum msg %channel Thankyou for using AdiSamLink $GetSamLinkVersion()
  }
}

alias TestSamLink {
  ;This is called from the RMDPopup script
  sockclose loopbacktest
  if ( $sock(saminput,0) == 1) {
    echo -s Sam listener port is running
  } else {
    echo -s Sam listener port is not running, starting it now
    startSamSocket
    if ( $sock(saminput,0) == 1) {
      echo -s  => Sam listener port started ok
    } else {
      echo -s  => Sam listener port failed to start!!! Is something else using port 50501?  Firewall issues?
      return
    }
  }
  echo -s Testing localhost loopbacktest connection .. (this should appear in the dj's channel)
  sockopen loopbacktest 127.0.0.1 50501
  sleep 1 /sockwrite -n loopbacktest GET /message?time=120&data=Test+message+from+AdiIRC HTTP/1.0
  sleep 2 /sockclose loopbacktest
}

alias -l diagnostics if (%diagnostics) scon 1 echo -s AdiSamLink: $1-

alias -l getNextActiveDJ {
  ;getNextActiveDJ(scon number, channel)
  ;get next active DJ (has DJ in their nick, not guesting, not merc, not self) or returns simply DJ
  diagnostics looking for DJ...
  scon $1
  var %channel = $2
  var %result = DJ
  var %n = $nick(%channel,0)
  while (%n > 0) {
     var %nick = $nick(%channel,%n)
     diagnostics Checking nick %n = %nick
     var %valid = true
     if ($count(%nick,DJ) == 0 ) %valid = false
     if ($count(%nick,guest) > 0) %valid = false
     if ($count(%nick,DJ`Merc) > 0) %valid = false
     if (%nick == $me) %valid = false
     if (%valid == true) %result = %nick
     dec %n
  }
  diagnostics DJ in DJ channel is %result
  scon -r
  return %result
}

alias -l simplehash {
  ;Return simple case-independent hash of $1 as a smallish int
  ;TODO make a better one way hash with higher entropy
  var %acc =0
  var %n = $len($1)
  while (%n > 0) {
     %acc = $calc( $asc($mid($1,%n,1)) % 32 * %n + %acc )
     dec %n
  }
  return %acc
}
